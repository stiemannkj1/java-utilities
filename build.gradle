plugins {
  id "java"
  id "maven-publish"
  id("com.diffplug.gradle.spotless") version "4.5.1"
}

group = "com.github.stiemannkj1"
version = "0.5.1-SNAPSHOT"

repositories {
  mavenCentral()
  maven {
    url "https://repo.gradle.org/gradle/libs-releases/"
  }
}

final def mavenLocalPath = "${System.getProperty("user.home")}/.m2/repository"

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      url "file:///$mavenLocalPath"
    }
  }
}

// Work around https://github.com/gradle/gradle/issues/2798 to publish full
// checksum files to JitPack.
tasks.replace("publishToMavenLocal")
    .dependsOn(tasks.publishAllPublicationsToMavenRepository)
    .doFirst {
      println("Published artifacts to $mavenLocalPath")
    }

compileJava {
  options.compilerArgs.addAll(["--release", "8"])
}

dependencies {
  final def minSupportedGradleVersion = "5.6.4"
  final def gradleDir = ".gradle/gradle/lib"
  compileOnly fileTree(dir: gradleDir, include: "groovy-all-*2.5.4.jar")
  compileOnly files("$gradleDir/gradle-core-${minSupportedGradleVersion}.jar")
  compileOnly files("$gradleDir/gradle-core-api-${minSupportedGradleVersion}.jar")
  compileOnly files("$gradleDir/gradle-model-core-${minSupportedGradleVersion}.jar")
  compileOnly files("$gradleDir/gradle-logging-${minSupportedGradleVersion}.jar")
  compileOnly files("$gradleDir/gradle-tooling-api-${minSupportedGradleVersion}.jar")
  compileOnly files("$gradleDir/gradle-base-services-${minSupportedGradleVersion}.jar")
  compileOnly files("$gradleDir/plugins/gradle-language-jvm-${minSupportedGradleVersion}.jar")
  compileOnly files("$gradleDir/plugins/gradle-platform-jvm-${minSupportedGradleVersion}.jar")
  compileOnly files("$gradleDir/plugins/gradle-plugins-${minSupportedGradleVersion}.jar")
  testImplementation platform("org.junit:junit-bom:5.9.1")
  testImplementation "org.junit.jupiter:junit-jupiter"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
  systemProperty("project.build.dir", project.buildDir.absolutePath)
  systemProperty("dev.stiemannkj1.util.Assert.enabled", true)
  useJUnitPlatform()
}

spotless {
  java {
    googleJavaFormat()
    targetExclude("**/*Generated.java")
  }
  groovyGradle {
    greclipse().configFile("greclipse.properties")
  }
}
