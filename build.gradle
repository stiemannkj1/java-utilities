plugins {
  id "java"
  id "maven-publish"
  id("com.diffplug.gradle.spotless") version "4.5.1"
}

group = "com.github.stiemannkj1"
version = "0.5.1-SNAPSHOT"

repositories {
  mavenCentral()
  maven {
    url "https://repo.gradle.org/gradle/libs-releases/"
  }
}

final def mavenLocalPath = "${System.getProperty("user.home")}/.m2/repository"

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      url "file:///$mavenLocalPath"
    }
  }
}

// Work around https://github.com/gradle/gradle/issues/2798 to publish full
// checksum files to JitPack.
tasks.replace("publishToMavenLocal")
    .dependsOn(tasks.publishAllPublicationsToMavenRepository)
    .doFirst {
      println("Published artifacts to $mavenLocalPath")
    }

compileJava {
  options.getRelease().set(8)
}

dependencies {
  final def minSupportedGradleVersion = "5.6.4"
  compileOnly "org.codehaus.groovy:groovy-all:2.5.23"
  compileOnly "org.gradle:gradle-core:$minSupportedGradleVersion"
  compileOnly "org.gradle:gradle-core-api:$minSupportedGradleVersion"
  compileOnly "org.gradle:gradle-model-core:$minSupportedGradleVersion"
  compileOnly "org.gradle:gradle-logging:$minSupportedGradleVersion"
  compileOnly "org.gradle:gradle-tooling-api:$minSupportedGradleVersion"
  compileOnly "org.gradle:gradle-base-services:$minSupportedGradleVersion"
  compileOnly "org.gradle:gradle-language-jvm:$minSupportedGradleVersion"
  compileOnly "org.gradle:gradle-platform-jvm:$minSupportedGradleVersion"
  compileOnly "org.gradle:gradle-plugins:$minSupportedGradleVersion"
  testImplementation platform("org.junit:junit-bom:5.9.1")
  testImplementation "org.junit.jupiter:junit-jupiter"
}

test {
  systemProperty("project.build.dir", project.buildDir.absolutePath)
  systemProperty("dev.stiemannkj1.util.Assert.enabled", true)
  useJUnitPlatform()
}

spotless {
  java {
    googleJavaFormat()
    targetExclude("**/*Generated.java")
  }
  groovyGradle {
    greclipse().configFile("greclipse.properties")
  }
}
