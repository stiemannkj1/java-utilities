plugins {
  id 'java'
  id 'maven-publish'
  id("com.diffplug.gradle.spotless") version "4.5.1"
}

group = 'com.github.stiemannkj1'
version = '0.2.1'

repositories {
  mavenCentral()
}

final def mavenLocalPath = "${System.getProperty("user.home")}/.m2/repository"

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      url "file:///$mavenLocalPath"
    }
  }
}

// Work around https://github.com/gradle/gradle/issues/2798 to publish full
// checksum files to JitPack.
tasks.replace('publishToMavenLocal')
    .dependsOn(tasks.publishAllPublicationsToMavenRepository)
    .doFirst {
      println("Published artifacts to $mavenLocalPath")
    }

compileJava {
  options.getRelease().set(8)
}

dependencies {
  testImplementation platform('org.junit:junit-bom:5.9.1')
  testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
  useJUnitPlatform()
}

spotless {
  java {
    googleJavaFormat()
  }
  groovyGradle {
    greclipse().configFile("greclipse.properties")
  }
}
